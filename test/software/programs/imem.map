ENTRY(_enter)

MEMORY
{
  rom (irx!wa) : ORIGIN = 0x20000000, LENGTH = 2048
  ram (arw!xi) : ORIGIN = 0x80000000, LENGTH = 2048
}

SECTIONS
{
  .text : {
    *(.text.unlikely .text.unlikely.*)
    *(.text.startup .text.startup.*)
    *(.text .text.*)
    *(.gnu.linkonce.t.*)
  } >rom

  .rodata : {
    *(.rdata)
    *(.rodata .rodata.*)
    *(.gnu.linkonce.r.*)
    . = ALIGN(8);
    *(.srodata.cst16)
    *(.srodata.cst8)
    *(.srodata.cst4)
    *(.srodata.cst2)
    *(.srodata .srodata.*)
  } >rom

  .data : ALIGN(8) {
    *(.data .data.*)
    *(.gnu.linkonce.d.*)
    . = ALIGN(8);
    PROVIDE( __global_pointer$ = . + 0x800 );
    *(.sdata .sdata.* .sdata2.*)
    *(.gnu.linkonce.s.*)
  } >ram AT>rom

  .tdata : ALIGN(8) {
    PROVIDE( __tls_base = . );
	  *(.tdata .tdata.* .gnu.linkonce.td.*)
  } >ram AT>rom
  
  .bss (NOLOAD): ALIGN(8) {
    *(.sbss*)
    *(.gnu.linkonce.sb.*)
    *(.bss .bss.*)
    *(.gnu.linkonce.b.*)
    *(COMMON)
  } >ram

   __stack_size = DEFINED(__stack_size) ? __stack_size : 256;
    PROVIDE(__stack_size = __stack_size); 

  .stack (NOLOAD) : ALIGN(16) {
    PROVIDE(metal_segment_stack_begin = .);
    . += __stack_size; /* Hart 0 */
    PROVIDE( _sp = . );
    PROVIDE(metal_segment_stack_end = .);
  } >ram
}
